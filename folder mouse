//folder mouse:use two potentiometers linked to sticks to position the mouse pen
#include <Mouse.h>
bool if_press=false;
double a1=0;                                //angles now(radian)
double a2=0;
double a1_b=0;                              //angles before
double a2_b=0;
double da1=0;
double da2=0;
double dx=0;
double dy=0;
double dx_b=0;                             //the coordinate changings before.add it with the changing this time to smooth the moving
double dy_b=0;
void mousepress()
{
   if(digitalRead(5)==LOW&&if_press==false)    //if the button is pressed but the mouse isn't in the pressed state
    {
      Mouse.press();
      if_press=true;
//      Serial.println("pressed!");
    }
    else if(digitalRead(5)==HIGH&&if_press==true)   //if the mouse is in the pressed state but the button has been released
    {
      Mouse.release();
      if_press=false;
    }
}
void setup() {
  pinMode(4,INPUT_PULLUP);
  pinMode(5,INPUT_PULLUP);
//  Serial.begin(9600);
  Mouse.begin();
}

void loop() {
  if(digitalRead(4)==LOW)    //the mouse pen is pressed on the desk,cusor should be moved
  {
    a1=analogRead(0)*0.004091-2.094;     //get the angle of the stick from the potential
    a2=analogRead(1)*0.004091-2.094;
    
    da1=a1-a1_b;
    da2=a2-a2_b;
    dx=da1*sin(a1)-da2*sin(a2);                  //calculate the movement on both axises
    dy=da1*cos(a1)+da2*cos(a2);
/*    Serial.print("dx");
    Serial.print(dx);
    Serial.print("       dy");
    Serial.println(dy);*/
    Mouse.move(300*dx+300*dx_b,300*dy+300*dy_b,0);
    a1_b=a1;
    a2_b=a2;
    dx_b=dx;
    dy_b=dy;
    mousepress();
    delay(5);
  }
  else                                      //cusor don't move,just renew the angles
  {
    Mouse.release();                               //the working of the mouse stopped,release the mouse
    a1_b=analogRead(0)*0.004091-2.094;     //get the angle of the stick from the potential
    a2_b=analogRead(1)*0.004091-2.094;
/*    Serial.print(a1_b);
    Serial.print("       ");
    Serial.println(a2_b);*/
    delay(20);
  }

}